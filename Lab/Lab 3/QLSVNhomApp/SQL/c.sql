--c.sql

-- C. VIẾT CÁC STORED PROCEDURES
---- i. Stored dùng để thêm mới dữ liệu (Insert) vào table NHANVIEN

IF EXISTS ( SELECT 1
FROM sys.procedures
WHERE name = 'SP_INS_PUBLIC_NHANVIEN' AND type = 'P')
BEGIN
    DROP PROCEDURE SP_INS_PUBLIC_NHANVIEN;
END
GO

CREATE PROCEDURE SP_INS_PUBLIC_NHANVIEN
    @MANV VARCHAR(20),
    @HOTEN NVARCHAR(100),
    @EMAIL VARCHAR(100),
    @LUONG FLOAT,
    @TENDN NVARCHAR(100),
    @MATKHAU VARCHAR(100)
AS
BEGIN
    SET NOCOUNT ON;

    IF EXISTS (SELECT 1
    FROM NHANVIEN
    WHERE MANV = @MANV)
    BEGIN
        RAISERROR('Nhân viên đã tồn tại.', 16, 1)
        RETURN
    END

    DECLARE @HASHED_PASSWORD VARBINARY(100)
    SET @HASHED_PASSWORD = HASHBYTES('SHA1', @MATKHAU)

    DECLARE @SQL NVARCHAR(MAX);
    SET @SQL = N'CREATE ASYMMETRIC KEY ' + QUOTENAME(@MANV) +
               N' WITH ALGORITHM = RSA_2048 ENCRYPTION BY PASSWORD = N''' + @MATKHAU + N''';';

    EXEC sp_executesql @SQL;

    DECLARE @ENCRYPTED_LUONG VARBINARY(500)
    SET @ENCRYPTED_LUONG = ENCRYPTBYASYMKEY(AsymKey_ID(@MANV), CONVERT(VARCHAR, @LUONG))

    INSERT INTO NHANVIEN
        (MANV, HOTEN, EMAIL, LUONG, TENDN, MATKHAU, PUBKEY)
    VALUES
        (@MANV, @HOTEN, @EMAIL, @ENCRYPTED_LUONG, @TENDN, @HASHED_PASSWORD, @MANV)

END
GO

---- ii. Stored dùng để truy vấn dữ liệu nhân viên (NHANVIEN)
IF EXISTS (SELECT 1
FROM sys.procedures
WHERE name = 'SP_SEL_PUBLIC_NHANVIEN' AND type = 'P'
)
BEGIN
    DROP PROCEDURE SP_SEL_PUBLIC_NHANVIEN;
END
GO

CREATE PROCEDURE SP_SEL_PUBLIC_NHANVIEN
    @TENDN NVARCHAR(100),
    @MK VARCHAR(100)
AS
BEGIN
    SET NOCOUNT ON;

    SELECT MANV, HOTEN, EMAIL, CONVERT(FLOAT, (CONVERT(varchar, DECRYPTBYASYMKEY(ASYMKEY_ID(PUBKEY), LUONG, CONVERT(nvarchar, @MK))))) AS LUONGCB
    FROM NHANVIEN
    WHERE TENDN = @TENDN AND MATKHAU = HASHBYTES('SHA1', @MK)
END
GO